&ACCESS RVO1
DEF Mill_FrameThreshold( )
   ;FOLD DECL
      DECL E_ProfileType eProfileType
      DECL E_ProfilePlace eProfilePlace
      DECL REAL rThresholdLength
      DECL REAL rRightDrainageX,rLeftDrainageX
   ;ENDFOLD
   ;FOLD INI
      
      ;Get data from ProcessData structure
      eProfileType = gstProcessData.eProfileType
      eProfilePlace = gstProcessData.eProfilePlace
      rThresholdLength = gstProcessData.rThresholdLength
      
      ;Profile type validation
      SWITCH eProfileType
         CASE #Threshold75,#Threshold90
            ;Correct process data
         DEFAULT
            HALT ;Invalid process data
      ENDSWITCH
      
      ;Profile placement validation
      IF (eProfilePlace<>#Bottom) THEN
         HALT ;Invalid process data
      ENDIF
      
      ;Threshold drainage - X coordinate
      rRightDrainageX = grShortProfileRefX + rDrainageEdgeDist
      rLeftDrainageX = grShortProfileRefX + rThresholdLength - rDrainageEdgeDist
      
   ;ENDFOLD
   
   BAS(#INITMOV)
   
   ;Go above stations
   ChangeStation(#AboveStations,#Idle)
   
   ;Mill table scavenge ON
   ;MillTableScavenge(TRUE)
   
   ;Get the right tool
   ChangeSpindlesTools(#Cutter5,#Empty)
   
   ;Drainage milling
   ChangeStation(#MillThresholdHSD,#Idle)
   Spindle(#eRun,#SpindleHSD,#Cutter5)
   ThresholdMillDrainage(rRightDrainageX)
   ThresholdMillDrainage(rLeftDrainageX)
   Spindle(#eStop)
   ChangeStation(#AboveStations,#Idle)
   
   ;Mill table scavenge OFF
   ;MillTableScavenge(FALSE)
   
   ;Go home position
   ChangeStation(#HomePos,#Idle)
   
END