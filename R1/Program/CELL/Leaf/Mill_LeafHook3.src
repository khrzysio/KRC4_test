&ACCESS RVEO
&REL 5
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program\LeafProfiles
DEF Mill_LeafHook3( )
   ;FOLD DECL
      DECL ST_Dim3 stScrew[10],stPocket[3]
      DECL ST_Dim3 stScrewTemp,stPocketTemp
      DECL ST_Dim3 stHandle,stHandleTemp,stKey,stKeyTemp
      DECL INT iScrew,iPocket
   ;ENDFOLD
   ;FOLD INI
      
      ;stScrew[]
      FOR iScrew=1 TO iScrewSize STEP 1
         SetScrewData(iScrew,stScrewTemp)
         stScrew[iScrew] = stScrewTemp
      ENDFOR
      
      ;stPocket[]
      FOR iPocket=1 TO iPocketSize STEP 1
         SetPocketData(iPocket,stPocketTemp)
         stPocket[iPocket] = stPocketTemp
      ENDFOR
      
      ;stKey,stHandle
      SetHandleKeyData(stHandleTemp,stKeyTemp)
      stHandle = stHandleTemp
      stKey = stKeyTemp
      
   ;ENDFOLD
   
   TODO

   BAS(#INITMOV)
   
   ChangeStation(#AboveStations,#Idle)
   HALT
   
   ;Tool Cutter16
   ChangeStation(#ToolMagazine,#Idle)
   ToolMagazine(#Change,#SpindleHSD,#Cutter16)
   ChangeStation(#AboveStations,#Idle)
   HALT
   
   ;Handle, key milling
   ChangeStation(#MillHoriCenterHSD,#Idle)
   Spindle(#eRun,#SpindleHSD,17500)
   LeafMillHandle(stHandle)
   LeafMillKey(stKey)
   Spindle(#eStop)
   ChangeStation(#AboveStations,#Idle)
   HALT
   
   ;Pocket milling
   ChangeStation(#MillVertRightHSD,#Idle)
   Spindle(#eRun,#SpindleHSD,17500)
   MillLockPocket(stPocket[1],stAuxPocketDim,1)
   MillLockPocket(stPocket[2],stMainPocketDim,2)
   MillLockPocket(stPocket[3],stAuxPocketDim,1)
   Spindle(#eStop)
   ChangeStation(#AboveStations,#Idle)
   HALT
   
   ;Tool Drill3
   ChangeStation(#ToolMagazine,#Idle)
   ToolMagazine(#Change,#SpindleHSD,#Drill3)
   ChangeStation(#AboveStations,#Idle)
   HALT
   
   ;Screw drilling
   ChangeStation(#MillVertLeftHSD,#Idle)
   Spindle(#eRun,#SpindleHSD,6000)
   FOR iScrew=1 TO iScrewSize STEP 1
      DrillHole(#Drill3,stScrew[iScrew])
   ENDFOR
   Spindle(#eStop)
   ChangeStation(#AboveStations,#Idle,#MillVertRightHSD)
   
   ChangeStation(#HomePos,#Idle)
   
END

DEF SetScrewData(iScrew:IN,stScrewOut:OUT)
   ;FOLD DECL
      DECL INT iScrew,iScrewDAT
      DECL E_ProfilePlace eProfilePlace
      DECL E_ProfileType eProfileType
      DECL REAL rProfileLength
      DECL ST_Dim3 stScrewTemp,stScrewOut
   ;ENDFOLD (DECL)
   
   ;Take data from ProcessData structure
   eProfileType = gstProcessData.eProfileType
   eProfilePlace = gstProcessData.eProfilePlace
   rProfileLength = gstProcessData.rProfileLength
   
   ;DAT file screw number
   SWITCH eProfilePlace
      CASE #Left
         iScrewDAT = iScrewSize - (iScrew-1)
      CASE #Right
         iScrewDAT = iScrew
      DEFAULT
         HALT
   ENDSWITCH
   
   ;Take the right profile data
   SWITCH eProfileType
      CASE #Leaf75
         ;stScrewTemp = stScrewLeaf75[iScrewDAT]
      CASE #Leaf90
         stScrewTemp = stScrewLeaf90[iScrewDAT]
      DEFAULT
         HALT 
   ENDSWITCH
   
   ;Initialization of screw data
   stScrewOut = stScrewTemp
   
   ;Correction of X coordinate
   SWITCH eProfilePlace
      CASE #Left
         stScrewOut.rX = grLongProfileRefX + stScrewTemp.rX
      CASE #Right
         stScrewOut.rX = grLongProfileRefX + rProfileLength - stScrewTemp.rX
      DEFAULT
         HALT
   ENDSWITCH
   
   ;Screw hole depth
   stScrewOut.rZ = rScrewHoleDepth
   
END

DEF SetPocketData(iPocket:IN,stPocketOut:OUT)
   ;FOLD DECL
      DECL INT iPocket,iPocketDAT
      DECL E_ProfilePlace eProfilePlace
      DECL E_ProfileType eProfileType
      DECL REAL rProfileLength
      DECL ST_Dim3 stPocketTemp,stPocketOut
   ;ENDFOLD (DECL)
   
   ;Take data from ProcessData structure
   eProfileType = gstProcessData.eProfileType
   eProfilePlace = gstProcessData.eProfilePlace
   rProfileLength = gstProcessData.rProfileLength
   
   ;DAT file pocket number
   SWITCH eProfilePlace
      CASE #Right
         iPocketDAT = iPocketSize - (iPocket-1)
      CASE #Left
         iPocketDAT = iPocket
      DEFAULT
         HALT
   ENDSWITCH
   
   ;Take the right profile data
   SWITCH eProfileType
      CASE #Frame75
         stPocketTemp = stPocketLeaf75[iPocketDAT]
      CASE #Frame90
         stPocketTemp = stPocketLeaf90[iPocketDAT]
      DEFAULT
         HALT 
   ENDSWITCH
   
   ;Initialization of pocket data
   stPocketOut = stPocketTemp
   
   ;Correction of X coordiate
   SWITCH eProfilePlace
      CASE #Left
         stPocketOut.rX = grLongProfileRefX + stPocketTemp.rX
      CASE #Right
         stPocketOut.rX = grLongProfileRefX + rProfileLength - stPocketTemp.rX
      DEFAULT
         HALT
   ENDSWITCH
   
END