&ACCESS RV1
DEF Cut_FrameRight()
   ;FOLD DECL
      DECL E_ProfileType eProfileType
      DECL E_ProfilePlace eProfilePlace
      DECL REAL rProfileLength,rRightCutX,rLeftCutX
      DECL ST_Dim3 stScrewTemp
      DECL ST_Dim3 stThresholdScrew[2]
      DECL INT iScrew
   ;ENDFOLD
   ;FOLD INI
      
      ;Get data from ProcessData structure
      eProfileType = gstProcessData.eProfileType
      eProfilePlace = gstProcessData.eProfilePlace
      rProfileLength = gstProcessData.rProfileLength
      
      ;Profile type validation
      SWITCH eProfileType
         CASE #Frame75,#Frame90
            ;Correct process data
         DEFAULT
            HALT ;Invalid process data
      ENDSWITCH
      
      ;Profile placement validation
      IF (eProfilePlace<>#Right) THEN
         HALT ;Invalid process data
      ENDIF
      
      ;Thresold screws
      FOR iScrew=1 TO 2 STEP 1
         
         ;Screw hole distances
         stScrewTemp = ThresholdData(iScrew)
         
         ;Screw hole coordinates
         stThresholdScrew[iScrew].rX = grProfileRefX + rProfileLength - stScrewTemp.rX
         stThresholdScrew[iScrew].rY = stScrewTemp.rY
         
      ENDFOR
      
      ;Cutting - X coordinate
      rRightCutX = grLongProfileRefX
      rLeftCutX = grLongProfileRefX + rProfileLength
      
   ;ENDFOLD
   
   BAS(#INITMOV)
   
   ;Go above stations
   ChangeStation(#AboveStations,#Idle)
   
   ;Get the right tool
   ChangeSpindlesTools(#Drill3,#Empty)
   
   ;Mill table scavenge ON
   ;MillTableScavenge(TRUE)
   
   ;Drill threshold screws
   ChangeStation(#MillVertLeftHSD,#Idle)
   Spindle(#eRun,#SpindleHSD,#Drill3)
   DrillHole(#Drill3,stThresholdScrew[1],1)
   DrillHole(#Drill3,stThresholdScrew[2],1)
   Spindle(#eStop)
   ChangeStation(#AboveStations,#Idle)
   
   ;Cut Left Side
   ChangeStation(#CutLeft,#Idle)
   CutLeft(rLeftCutX,90)
   ChangeStation(#AboveStations,#Idle)
   
   ;Cut Right Side
   ChangeStation(#CutRight,#Idle)
   CutRight(rRightCutX,45)
   ChangeStation(#AboveStations,#Idle)
   
   ;Mill table scavenge OFF
   ;MillTableScavenge(FALSE)
   
   ;Go home position
   ChangeStation(#HomePos,#Idle)
   
END